Algoritmo ReservaVuelos
    definir usuarios, provincias, reservasCompra, horariosVuelo, arrayEquipajesGuardados, opcionesEquipaje, elementoABuscar,pausa,finalizar como cadena
    definir opcionMenu, origen, destino, horario, codigosDeReserva,cantidadReservas, cantidadUsuarios, opcionValija, opcionCantidad, codigoReservaAsignado, cargaDeEquipaje, encontrarIndiceDeReserva, indiceEquipajes, numeroDeUsuario como entero
    definir retorno, registro, login, validaAdminUser como Logico
	definir costoTotal, tarifaBase, costoPasaje, costoFinalEquipaje, iva como real
	
	dimension usuarios[100,6] // Almacenamiento de los datos de usuarios registrados
    dimension reservasCompra[100,5] // Almacenamiento info sobre compra de pasaje
	dimension opcionesEquipaje[4,3] // muestra las opciones de equipaje 
	dimension arrayEquipajesGuardados[100,3] //almacenamiento del equipaje cargado por los usuarios
	dimension provincias[24,2] //array or?genes/destinos + zona tarifaria
	
	numeroDeUsuario<-0
	codigoReservaAsignado<-0
	indiceEquipajes<-0
	codigosDeReserva<- 1000
    cantidadUsuarios <- 0
	cantidadReservas <- 0
	tarifaBase<- 10000
	iva<-1.21
    costoPasaje<-0
	costoFinalEquipaje<-0
	login<- falso
	
    opcionesEquipaje[0,0] <- "Equipaje de mano"
    opcionesEquipaje[0,1] <- "Hasta 5 kg"
    opcionesEquipaje[0,2] <- "0" 
	
    opcionesEquipaje[1,0] <- "Valija chica"
    opcionesEquipaje[1,1] <- "Hasta 10 kg"
    opcionesEquipaje[1,2] <- "1000"  
    
    opcionesEquipaje[2,0] <- "Valija mediana"
    opcionesEquipaje[2,1] <- "Hasta 15 kg"
    opcionesEquipaje[2,2] <- "2000" 
	
	opcionesEquipaje[3,0] <- "Valija grande"
    opcionesEquipaje[3,1] <- "Hasta 20 kg"
    opcionesEquipaje[3,2] <- "3000" 
	
	provincias[0, 0] <- "Buenos Aires" //Se dividieron las provincias en 6 zonas geograficas, y luego se usa esto como criterio para calcular el costo del pasaje
    provincias[0, 1] <- "1"			 // multiplicando el n?mero de zona de origen por una tarifa base y sumando  esto a destino*tarifaBase
    provincias[1, 0] <- "Cordoba"	// la zona 1 es la zona centro del pa?s, zona 2 litoral, zona 3 cuyo, zona 4/5 norte, zona 6 patagonia
    provincias[1, 1] <- "1"			//con esta l?gica los vuelos son siempre m?s baratos entre provincias de la misma zona y a la vez aumentan de precio al alejarse
    provincias[2, 0] <- "Santa Fe"	//de la zona centro.
    provincias[2, 1] <- "1"
    provincias[3, 0] <- "Mendoza"
    provincias[3, 1] <- "3"
    provincias[4, 0] <- "Tucuman"
    provincias[4, 1] <- "4"
    provincias[5, 0] <- "Entre Rios"
    provincias[5, 1] <- "2"
    provincias[6, 0] <- "Salta"
    provincias[6, 1] <- "4"
    provincias[7, 0] <- "Chaco"
    provincias[7, 1] <- "5"
    provincias[8, 0] <- "Corrientes"
    provincias[8, 1] <- "2"
    provincias[9, 0] <- "Santiago del Estero"
    provincias[9, 1] <- "4"
    provincias[10, 0] <- "San Juan"
    provincias[10, 1] <- "3"
    provincias[11, 0] <- "Jujuy"
    provincias[11, 1] <- "4"
    provincias[12, 0] <- "Rio Negro"
    provincias[12, 1] <- "6"
    provincias[13, 0] <- "Neuquen"
    provincias[13, 1] <- "6"
    provincias[14, 0] <- "Formosa"
    provincias[14, 1] <- "5"
    provincias[15, 0] <- "Chubut"
    provincias[15, 1] <- "6"
    provincias[16, 0] <- "Misiones"
    provincias[16, 1] <- "2"
    provincias[17, 0] <- "San Luis"
    provincias[17, 1] <- "1"
    provincias[18, 0] <- "La Rioja"
    provincias[18, 1] <- "3"
    provincias[19, 0] <- "Catamarca"
    provincias[19, 1] <- "3"
    provincias[20, 0] <- "La Pampa"
    provincias[20, 1] <- "1"
    provincias[21, 0] <- "Santa Cruz"
    provincias[21, 1] <- "6"
    provincias[22, 0] <- "Tierra del Fuego"
    provincias[22, 1] <- "6"
    provincias[23, 0] <- "CABA"
    provincias[23, 1] <- "1"
    
    dimension horariosVuelo[3] 
    horariosVuelo[0] <- "Salida 8:00  / Llegada 11:00"  
    horariosVuelo[1] <- "Salida 12:00 / Llegada 15:00"
    horariosVuelo[2] <- "Salida 16:00 / Llegada 19:00"
	
	mostrar "|------------------------------|"
	mostrar "| *** Aerolineas FivePoint *** |"
	mostrar "|------------------------------|"
	mostrar "|  Se recomienda agrandar esta |"
	mostrar "| ventana para visualizar el   |"
	mostrar "|   programa correctamente     |"
	mostrar "|------------------------------|"
	mostrar "|      Presione ENTER para     |"
	mostrar "|         comenzar...          |"
	mostrar "|------------------------------|"
    Leer pausa
	Limpiar Pantalla
	
    Repetir
		opcionMenu <- menu()
		Segun opcionMenu
			
			1:  
				
				registrarUsuario(usuarios, cantidadUsuarios,numeroDeUsuario)
				
				
			2:	login<-validarLogin(usuarios,cantidadUsuarios)
				si login==Verdadero
					Limpiar Pantalla
					mostrar "|---------------|"
					mostrar "|LOGGIN ACEPTADO|"
					mostrar "|---------------|"
				SiNo
					mostrar "|---------------------------------------|"
					mostrar "|          !!!!ERROR!!!!                |"
					Mostrar "|     DNI O CONTRASEÑA INVALIDOS.       |"
					Mostrar "|  Si no se registro, HAGALO PRIMERO!!  |"
					mostrar "|---------------------------------------|"
				FinSi
				
			3: 
				si login==verdadero
					reservas(cantidadReservas, provincias, horariosVuelo, reservasCompra, codigosDeReserva, costoPasaje, tarifaBase)
					cargaDeEquipaje<-costoEquipaje(opcionesEquipaje, iva, arrayEquipajesGuardados, costoFinalEquipaje, indiceEquipajes)
					mostrarCostoFinal(costoPasaje, costoFinalEquipaje, iva)
					
				SiNo
					Limpiar Pantalla
					mostrar "|-----------------------------------------------------|"
					mostrar "|  SI QUERES RESERVAR, PRIMERO REGITRARSE Y LOGEARSE  |"
					mostrar "|-----------------------------------------------------|"
				finsi
				
			4:	encontrarIndiceDeReserva<-buscarElemento(reservasCompra,cantidadReservas,3)
				
				
				mostrar "Esta es su Reserva:"
				mostrar "Origen    -  Destino   -   Horario    -    cod. Reserva   -  Costo Pasaje"
				mostrarArrayEnIndiceEspecifico(encontrarIndiceDeReserva,reservasCompra,cantidadReservas,5)
				mostrar "Tipo de equipaje    -  cantidad   -   Costo Equipaje"
				mostrarArrayEnIndiceEspecifico(encontrarIndiceDeReserva,arrayEquipajesGuardados,indiceEquipajes,3)
				
			5:
				adminUser(usuarios, cantidadUsuarios,6)					
				
			6:  Limpiar Pantalla
				
				Repetir
					
					Escribir "ESCRIBA *SALIR* PARA FINALIZAR EL SERVICIO"
					Leer finalizar
					finalizar=Mayusculas(finalizar)
					
					si finalizar=="SALIR" Entonces
						
						mostrar "--------------------------------------------------"
						mostrar " *** Gracias por utilizar nuestros servicios ***"
						mostrar "--------------------------------------------------"
						
						mostrar "--------------------------------------------------"
						mostrar "        ***** AEROLINEAS FIVEPOINT *****"
						mostrar "--------------------------------------------------"
					FinSi
					
				Mientras Que finalizar<>"SALIR"
				
		FinSegun
	Mientras Que opcionMenu<>6
	
FinAlgoritmo

Funcion return<- menu() //funcion para validar las opciones del menu
	Definir opcionM, return Como Entero
	Repetir
		mostrar "|------------------------------|"
		mostrar "| *** Aerolineas FivePoint *** |"
		mostrar "|------------------------------|"
		Escribir "1--> REGISTRARSE" 
		Escribir "2--> LOGGIN"
		Escribir "3--> CONSULTE DESTINOS Y RESERVE VUELOS"
		Escribir "4--> VER EL ESTADO DE LA RESERVA" 
		Escribir "5--> ADMINISTRADOR (ACCESO RESTRINGIDO)" //usuario con login y pass pre-definidos
		Escribir "6--> SALIR"
		Leer opcionM
		Limpiar Pantalla
		si opcionM<=0 o opcionM>7
			Escribir "!!!!ERROR,INGRESE UNA OPCION VALIDA!!!!"
			Escribir ""
		FinSi
	Mientras Que opcionM<1 y opt>6
	return<-opcionM
FinFuncion

SubProceso  registrarUsuario(usuarios Por Referencia, cantidadUsuarios Por Referencia,numeroDeUsuario Por Referencia)
    definir nombre, apellido, edad, dni, password1, password2,pausa como cadena
	definir validacion como entero
    
	mostrar "-----------------------------------------------"
	mostrar "PARA REGISTRARSE COMPLETE LOS SIGUIENTES DATOS"
	mostrar "-----------------------------------------------"
	Escribir ""
    Mostrar Sin Saltar"Ingresar nombre:"
    leer nombre
    Mostrar Sin Saltar "Ingresar apellido:"
    leer apellido
    
    Repetir
        Mostrar Sin Saltar "Ingresar su DNI:"
        leer dni
    Hasta Que Longitud(dni) >= 6 Y Longitud(dni) <= 8 //validacion DNI entre 6 y 8 caracteres
    
	Repetir
		Mostrar Sin Saltar "Ingrese su edad:" 
		leer edad 
		validacion<-ConvertirANumero(edad)	
		
	Mientras Que validacion<=17 //validaci?n usuario mayor de edad
	
	Repetir
		mostrar "Ingrese su contraseña"
		leer password1
		mostrar "Ingrese nuevamente su contraseña"
		leer password2
		si password1<>password2
			mostrar "ERROR, LAS CONTRASEÑAS NO COINCIDEN."
			Mostrar "INTENTE NUEVAMENTE"
		FinSi
		
	Mientras Que password1<>password2
	
	numeroDeUsuario<-numeroDeUsuario + 1
	
    usuarios[cantidadUsuarios, 0] <- nombre 
    usuarios[cantidadUsuarios, 1] <- apellido
    usuarios[cantidadUsuarios, 2] <- dni
    usuarios[cantidadUsuarios, 3] <- edad
	usuarios[cantidadUsuarios, 4] <- password2
	usuarios[cantidadUsuarios, 5] <- convertirATexto(numeroDeUsuario)//se agreg? un nro de usuario para despue?s hacer ordenamiento alfab?tico y con el nro de usuario dejar el array en su oprden original
	
	Limpiar Pantalla
	mostrar "-------------------------------------"
	mostrar "SU REGISTRO FUE COMPLETADO CON EXITO."
	mostrar "-------------------------------------"
	
	mostrar "-------------------------------------"
	Mostrar "PRESIONE ENTER PARA CONTINUAR"
	mostrar "-------------------------------------"
	Leer pausa
	Limpiar Pantalla
	
    cantidadUsuarios <- cantidadUsuarios + 1
	
FinSubProceso

Funcion return<- validarLogin(usuarios,n)
	Definir i Como Entero
	definir dni, password como cadena
	definir dniEncontrado, return Como Logico
	Limpiar Pantalla
	
	mostrar "-----------------------------------------------"
	mostrar "PARA LOGGEARSE COMPLETE LOS SIGUIENTES DATOS"
	mostrar "-----------------------------------------------"
	Escribir ""
	
	mostrar "Ingrese su DNI"
	leer dni
	mostrar "Ingrese su contraseña"
	leer password
	
	i<-0;
	dniEncontrado <- Falso;
	Mientras i <= n-1 y no dniEncontrado
		si usuarios[i,2] == dni y usuarios[i,4]==password Entonces
			dniEncontrado <- Verdadero 
			mostrar "Se ha loggeado correctamente!"
			
		FinSi
		i <- i +1; 
	FinMientras
	return	<- dniEncontrado
	
FinFuncion

subproceso reservas(cantidadReservas por referencia, provincias, horariosVuelo, reservasCompra Por Referencia, codigosDeReserva Por Referencia, costopasaje Por Referencia, tarifaBase por referencia)
	definir codigoReservaAsignado,i, origen, destino, horario como entero
	definir pausa como cadena
	
	mostrar "------------------------------"
	mostrar "     Reserva de Vuelos"
    mostrar "------------------------------"
    mostrar "    Seleccione el origen:"
	mostrar "------------------------------"
    
	para i <- 0 hasta 23 hacer //se muestra el listado de provincias para elegir el origen
		
		mostrar i+1, ". ", provincias[i,0], "     Zona Tarifaria: " provincias[i,1]
		
	FinPara
	
	mostrar "------------------------------"
    mostrar "    Seleccione el origen:"
	mostrar "------------------------------"
	
	Repetir
		leer origen 
	Mientras Que origen<1 o origen>24
	
    mostrar "------------------------------"
    mostrar "    Seleccione el destino:"
	mostrar "------------------------------"// el listado de provincias debe seguir en pantalla
	
	Repetir
		Repetir
			leer destino 
		Mientras Que destino<1 o destino>24
		si destino == origen entonces
			mostrar "-----------------------------------------------------"
			mostrar "Error, debe seleccionar un destino distinto al origen"
			mostrar "Origen: " origen
			mostrar "Vuelva a seleccionar un destino:"
			mostrar "-----------------------------------------------------"
		FinSi
	Mientras Que destino==origen //validaci?n para que no permita elegir mismo origen/destinos
	
	mostrar "--------------------------------"
	mostrar " Seleccione el horario de vuelo:"
	mostrar "--------------------------------"
	para i<-0 hasta 2
		mostrar  i+1,"     ", horariosVuelo[i] 
	FinPara
	
	Repetir
		leer horario 
	Mientras Que horario<1 o horario>3
	
	costoPasaje<- ConvertirANumero(provincias[origen-1,1])*tarifaBase + (convertirANumero(provincias[destino-1,1]) * tarifaBase)
	Limpiar Pantalla
	mostrar "------------------------------------------"
	mostrar " El costo de su vuelo es de: $" costoPasaje
	mostrar "------------------------------------------"
	mostrar "--------------------------------"
	mostrar "   Su codigo de reserva es: "
	mostrar "--------------------------------"
	
	codigosDeReserva<-  codigosDeReserva +1 //otorgar nro de reserva
	
	mostrar codigosDeReserva
	
	reservasCompra[cantidadReservas,0]<- (provincias[origen-1,0]) 
	reservasCompra[cantidadReservas,1]<- (provincias[destino-1,0])
	reservasCompra[cantidadReservas,2]<- (horariosVuelo[horario-1])
	reservasCompra[cantidadReservas,3]<- ConvertirATexto(codigosDeReserva)
	reservasCompra[cantidadReservas,4]<- ConvertirATexto(costoPasaje)
	
	cantidadReservas<- cantidadReservas + 1 
	
	mostrar "-----------------------------------------------------------"
	mostrar " Resumen de su reserva:"
	mostrar " Origen     Destino       Hora de salida / Hora de llegada"
	mostrar "-----------------------------------------------------------"
	mostrarArray(reservasCompra,cantidadReservas,3) 
	mostrar "-----------------------------------------------------------"
	Escribir "PRESIONE ENTER PARA CONTINUAR"
	Leer pausa
	Limpiar Pantalla
FinSubProceso

funcion return<-costoEquipaje(opcionesEquipaje, iva Por Referencia, arrayEquipajesGuardados,costoFinalEquipaje por referencia, indiceEquipajes Por Referencia)
	definir cantidadValija, opcionValija, return como entero
	definir  costoAnumero como real
	definir costoCadena como cadena
	cantidadValija<-0
	mostrar "---------------------"
	mostrar "Opciones de equipaje:"
    mostrar "---------------------"
    mostrar "Tipo               Peso                Costo"
    mostrar opcionesEquipaje[0,0], "  " opcionesEquipaje[0,1], "      " opcionesEquipaje[0,2] "	"
    mostrar opcionesEquipaje[1,0], "    " opcionesEquipaje[1,1], "     " opcionesEquipaje[1,2] "	 "
    mostrar opcionesEquipaje[2,0], "    " opcionesEquipaje[2,1], "     " opcionesEquipaje[2,2] "	"
	mostrar opcionesEquipaje[3,0], "     " opcionesEquipaje[3,1], "     " opcionesEquipaje[3,2] "	"
	mostrar "---------------------"
	mostrar "Elija una opcion de equipaje"
	mostrar "1- Equipaje de mano"
	mostrar "2- Valija 10 kg"
	mostrar "3- Valija 15 kg"
	mostrar "4- Valija 20 kg"
	
	Repetir
		leer opcionValija
	Mientras Que opcionValija < 1 o opcionValija > 4 //validacion
	
	mostrar "------------------------------------------"
	mostrar " Ingresar cantidad de valijas (3 maximo)"
	mostrar "------------------------------------------"
	
	Repetir
		leer cantidadValija
	Mientras Que cantidadValija<1 o cantidadValija>3
	
	costoCadena<- opcionesEquipaje[opcionValija -1,2] 
	
	costoFinalEquipaje <- cantidadValija * ConvertirANumero(costoCadena )
	
	mostrar "-------------------------------------------------------"
	mostrar "Resumen de su carga de equipaje"
	mostrar "-------------------------------------------------------"
	mostrar "Tipo de equipaje: " opcionesEquipaje[opcionValija-1,0]
	mostrar "Cantidad: " cantidadValija
	mostrar "Costo Total de quipaje : " costoFinalEquipaje
	mostrar "-------------------------------------------------------"
	
	arrayEquipajesGuardados[indiceEquipajes,0]<- opcionesEquipaje[opcionValija-1,0]
	arrayEquipajesGuardados[indiceEquipajes,1]<- ConvertirATexto(cantidadValija)
	arrayEquipajesGuardados[indiceEquipajes,2]<- ConvertirATexto(costoFinalEquipaje)
	
	indiceEquipajes <- indiceEquipajes +1 
	
    return<- costoFinalEquipaje
FinFuncion

SubProceso mostrarCostoFinal(costoPasaje Por Referencia, costoFinalEquipaje por referencia, iva por referencia)
	definir pausa como cadena
	
	mostrar "-------------------------------------------------------"
	mostrar "     ******** TICKET FINAL DE SU COMPRA *********"
	mostrar "-------------------------------------------------------"
	
	mostrar "Pasaje: " costoPasaje
	mostrar "Equipaje: " costoFinalEquipaje
	mostrar "Sub-total: " costoPasaje + costoFinalEquipaje
	mostrar "FINAL con IVA: $ " (costoPasaje + costoFinalEquipaje) * iva
	
	mostrar "--------------------------------------"
	Escribir "PRESIONE ENTER PARA CONTINUAR"
	mostrar "--------------------------------------"
	Leer pausa
	Limpiar Pantalla
FinSubProceso

SubProceso mostrarArray(array,n,m)
	definir i,j como entero
	definir elementoEncontrado Como Logico
	
	Para i<-0 Hasta n-1 Hacer
		Para j<-0 Hasta m-1 Hacer
			Escribir Sin Saltar array[i,j] "   "
		Fin Para
		Escribir ""
	Fin Para
FinSubProceso

Funcion return<- buscarElemento(array,n,columnaAbuscar) 
	Definir i, return Como Entero
	definir elementoEncontrado como logico
	definir elementoABuscar como cadena
	
	mostrar "Ingresar nro de RESERVA para ver el estado de su compra"
	leer elementoABuscar
	
	i<-0;
	elementoEncontrado <- Falso
	
	Mientras i <= n-1 y no elementoEncontrado
		si array[i,columnaAbuscar] == elementoABuscar Entonces
			elementoEncontrado <- Verdadero;
		SiNo
			i <- i +1; 
		FinSi
	FinMientras
	Si elementoEncontrado Entonces
		return <- i;
	SiNo
		Escribir "Error ingrese nro de reserva valido" ;
		elementoEncontrado<-Falso
		return<- i-1
	FinSi
FinFuncion

SubProceso mostrarArrayEnIndiceEspecifico(encontrarIndiceDeReserva Por Referencia,array,n,m)
	definir i,j como entero
	Para i<-0 Hasta n-1 Hacer
		Para j<-0 Hasta m-1 Hacer
			Escribir Sin Saltar array[encontrarIndiceDeReserva,j] "   "
		Fin Para
		Escribir ""
	Fin Para
FinSubProceso



SubProceso adminUser(usuarios, cantidadUsuarios Por referencia, 6)
    Definir opt, contador Como Entero
    Definir user, pass, verMenuNuevamente, pausa, apellido Como Cadena
    contador <- 0
	
    //-----------------------------//
    // user: admin                //
    //                            //
    // password: admin             //
    //----------------------------//

	
    Repetir
        Mostrar "Ingrese su nombre de usuario"
        Leer user
		user<-Minusculas(user)
        Mostrar "Ingrese su password"
        Leer pass
		pass<-Minusculas(pass)
        contador <- contador + 1
    Mientras Que (user <> "admin" O pass <> "admin") y contador < 3
	
    Limpiar Pantalla
	
    Repetir
        Si contador == 3 Entonces
            Escribir "Ha ingresado los datos de forma incorrecta 3 veces, vuelva a intentarlo más tarde..."
            Leer pausa
        SiNo
            Repetir
                Mostrar "Ingrese una opción"
                Mostrar "1 - Ver usuarios registrados en el sistema"
                Mostrar "2 - Buscar usuario registrado por apellido"
                Mostrar "3 - Visualizar usuarios por apellido en orden alfabético descendente"
                Mostrar "4 - Salir"  
				
                Leer opt
            Hasta Que opt >= 1 Y opt <= 4
			
            Segun opt Hacer
                1:
                    Escribir "  NOMBRE  -  APELLIDO  -  DNI  -  EDAD  "
                    mostrarArray(usuarios, cantidadUsuarios, 4)
					mostrar "--------------------------------------------------------"
                2:
                    Escribir Sin Saltar "Ingrese el apellido que desea buscar: "
                    Leer apellido
					apellido<-Minusculas(apellido)
                    buscarUsuarioRegistrado(usuarios, cantidadUsuarios, 4, apellido)
					mostrar "--------------------------------------------------------"
					
                3:
                    ordernarUsuariosDESC(usuarios, cantidadUsuarios, 6, 1)
                    mostrarArray(usuarios, cantidadUsuarios, 6)
                    ordernarArregloASC(usuarios, cantidadUsuarios, 6, 5) // Regresa a su orden original, ordenándolo por nro de usuario registrado
					mostrar "--------------------------------------------------------"
                4:
                    Escribir "Saliendo de modo administrador."
                    Escribir "Para operar nuevamente vuelva a loguearse."
					
            Fin Segun
        Fin Si
    Hasta Que opt = 4
	
FinSubProceso




SubProceso buscarUsuarioRegistrado(arreglo, n, m, apellido) //busca usuario registrado por apellido
	Definir fila, columna,i,j Como Entero 
	Definir encontrado Como Logico
	encontrado<-Falso
	
	Para i<-0 hasta n-1 Hacer
		Para j<-0 hasta m-1 Hacer
			si arreglo[i,j]==apellido Entonces
				fila<-i
				columna<-j
				encontrado<-Verdadero
				
			FinSi
		FinPara
	FinPara
	
	si encontrado == Verdadero Entonces
		Escribir "¡Se ha encontrado el apellido buscado!"
		Escribir "Los datos del usuario son: "
		Escribir "  NOMBRE  -  APELLIDO  -  DNI  -  EDAD  "
		Para j<-0  Hasta m-1 Hacer
			Escribir Sin Saltar arreglo[fila,j] " "
		FinPara
		Escribir ""
	SiNo
		escribir "El apellido buscado no se ha encontrado..."
	FinSi
FinSubProceso



SubProceso mostrarUsuarios(usuarios,n,m)
	definir i,j como entero
	Para i<-0 Hasta n-1 Hacer
		Para j<-0 Hasta m-1 Hacer
			Escribir Sin Saltar usuarios[i,j] "   "
		Fin Para
		Escribir ""
	Fin Para
FinSubProceso




SubProceso ordernarUsuariosDESC(array,n,m,columnaAOrdenar) // para ordenar usuarios Alfab?ticamente
	definir i,k como entero
	Definir aux Como cadena
	para i<-0 hasta n-2 Hacer 
		para k<-i+1 hasta n-1 Hacer 
			si array[i,1]>array[k,1] Entonces
				Para j<-0 Hasta m-1 Hacer
					aux <- array[i,j]
					array[i,j] <- array[k,j]
					array[k,j] <- aux
				Fin Para
			FinSi
		FinPara
	FinPara
FinSubProceso

SubProceso ordernarArregloASC(array,n,m,columnaAOrdenar) // para ordenar usuarios por nro de usuario (dps de ordenar alfab?ticamente)
	Definir aux Como Texto
	definir i, k como entero
	para i<-0 hasta n-2 Hacer 
		para k<-i+1 hasta n-1 Hacer 
			si array[i,columnaAOrdenar]>array[k,columnaAOrdenar] Entonces
				Para j<-0 Hasta m-1 Hacer
					aux <- array[i,j]
					array[i,j] <- array[k,j]
					array[k,j] <- aux
				Fin Para
			FinSi
		FinPara
	FinPara
FinSubProceso

